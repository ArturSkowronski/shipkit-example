project.ext.configurePom = { Project project, MavenPublication publication ->
    publication.pom.withXml {
        def root = asNode()
        root.appendNode('name', project.archivesBaseName)
        root.appendNode('packaging', 'jar')
        root.appendNode('url', "https://github.com/${rootProject.ext.gh_repository}")
        root.appendNode('description', project.description)

        def license = root.appendNode('licenses').appendNode('license')
        license.appendNode('name', 'The MIT License')
        license.appendNode('url', "https://github.com/${rootProject.ext.gh_repository}/blob/master/LICENSE")
        license.appendNode('distribution', 'repo')

        root.appendNode('scm').appendNode('url', "https://github.com/${rootProject.ext.gh_repository}.git")

        def issues = root.appendNode('issueManagement')
        issues.appendNode('url', "https://github.com/${rootProject.ext.gh_repository}/issues")
        issues.appendNode('system', 'GitHub issues')

        def ci = root.appendNode('ciManagement')
        ci.appendNode('url', "https://travis-ci.org/${rootProject.ext.gh_repository}")
        ci.appendNode('system', 'TravisCI')

        def developers = root.appendNode('developers')
        rootProject.ext.pom_developers.each {
            def split = it.split(':')
            assert split.length == 2
            def d = developers.appendNode('developer')
            d.appendNode('id', split[0])
            d.appendNode('name', split[1])
            d.appendNode('roles').appendNode('role', 'Core developer')
            d.appendNode('url', "https://github.com/${split[0]}")
        }

        def contributors = root.appendNode('contributors')
        rootProject.ext.pom_contributors.each {
            def split = it.split(':')
            assert split.length == 2
            def c = contributors.appendNode('contributor')
            c.appendNode('name', split[1])
            c.appendNode('url', "https://github.com/${split[0]}")
        }
    }
}
